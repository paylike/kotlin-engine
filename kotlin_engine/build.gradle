plugins {
	id 'com.android.library'
	id 'org.jetbrains.kotlin.android'
	id 'org.jetbrains.kotlin.plugin.serialization' version '1.5.30'
	id 'maven-publish'
	id 'com.diffplug.spotless' version '6.8.0'
}

def compose_version  = '1.2.0'

android {
	compileSdk 32
	defaultConfig {
		minSdk 26
		targetSdk 32
		vectorDrawables {
			useSupportLibrary true
		}
	}
	buildTypes {
		def localProperties = new Properties()
		debug {
			localProperties.load(new FileInputStream(rootProject.file('local.properties')))
			buildConfigField(
					'String',
					'PaylikeMerchantApiKey',
					(localProperties.containsKey('PaylikeMerchantApiKey')
					? localProperties.getProperty('PaylikeMerchantApiKey')
					: 'new String()'
					)
					)
		}
		release {
			localProperties.load(new FileInputStream(rootProject.file('local.properties')))
			buildConfigField(
					'String',
					'PaylikeMerchantApiKey',
					(localProperties.containsKey('PaylikeMerchantApiKey')
					? localProperties.getProperty('PaylikeMerchantApiKey')
					: 'new String()'
					)
					)
			minifyEnabled false
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = '1.8'
	}
	buildFeatures {
		compose true
	}
	composeOptions {
		kotlinCompilerExtensionVersion compose_version
	}
	packagingOptions {
		resources {
			excludes += '/META-INF/{AL2.0,LGPL2.1}'
		}
	}
}

dependencies {
	testImplementation 'junit:junit:4.13.2'
	implementation "androidx.compose.ui:ui:$compose_version"
	implementation "androidx.compose.material:material:$compose_version"
	implementation 'androidx.webkit:webkit:1.5.0'
	implementation 'org.jetbrains.kotlin:kotlin-reflect:1.7.10'
	api 'com.github.paylike:kotlin_luhn:1.0.6'
	api 'com.github.paylike:kotlin_client:1.0.2'
}

spotless {
	kotlin {
		target 'src/**/*.kt', 'src/**/*.kts'
		ktfmt().kotlinlangStyle()
		trimTrailingWhitespace()
		endWithNewline()
	}
	groovyGradle {
		target '*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}
build.dependsOn 'spotlessCheck'

afterEvaluate {
	publishing {
		publications {
			release(MavenPublication) {
				from components.release

				groupId = 'com.github.paylike'
				artifactId = 'kotlin_engine'
				version = '1.0.3'
			}
		}
	}
}
