plugins {
	id 'com.android.library'
	id 'org.jetbrains.kotlin.android'
	id 'org.jetbrains.kotlin.plugin.serialization' version '1.8.10'
	id 'maven-publish'
	id 'com.diffplug.spotless' version '6.8.0'
}

android {
	compileSdk 33
	defaultConfig {
		minSdk 26
		targetSdk 33
		vectorDrawables {
			useSupportLibrary true
		}
	}
	buildTypes {
		release {
			minifyEnabled false
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = '1.8'
	}
	buildFeatures {
		compose true
	}
	composeOptions {
		kotlinCompilerExtensionVersion '1.4.4'
	}
	packagingOptions {
		resources {
			excludes += '/META-INF/{AL2.0,LGPL2.1}'
		}
	}
}

dependencies {
	testImplementation 'junit:junit:4.13.2'
	implementation 'androidx.compose.ui:ui:1.4.0'
	implementation 'androidx.compose.material:material:1.4.0'
	implementation 'androidx.webkit:webkit:1.6.1'
	implementation 'org.jetbrains.kotlin:kotlin-reflect:1.7.10'
	api 'com.github.paylike:kotlin_luhn:1.0.7'
	api 'com.github.paylike:kotlin_client:1.0.3'
}

spotless {
	kotlin {
		target 'src/**/*.kt', 'src/**/*.kts'
		ktfmt().kotlinlangStyle()
		trimTrailingWhitespace()
		endWithNewline()
	}
	groovyGradle {
		target '*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}
build.dependsOn 'spotlessCheck'

afterEvaluate {
	publishing {
		publications {
			release(MavenPublication) {
				from components.release

				groupId = 'com.github.paylike'
				artifactId = 'kotlin_engine'
				version = '1.0.4'
			}
		}
	}
}
